/*
 * Copyright (c) 2014, Francis Galiegue (fgaliegue@gmail.com)
 *
 * This software is dual-licensed under:
 *
 * - the Lesser General Public License (LGPL) version 3.0 or, at your option, any
 *   later version;
 * - the Apache Software License (ASL) version 2.0.
 *
 * The text of this file and of both licenses is available at the root of this
 * project or, if you have the jar distribution, in directory META-INF/, under
 * the names LGPL-3.0.txt and ASL-2.0.txt respectively.
 *
 * Direct link to the sources:
 *
 * - LGPL 3.0: https://www.gnu.org/licenses/lgpl-3.0.txt
 * - ASL 2.0: http://www.apache.org/licenses/LICENSE-2.0.txt
 */

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://repo.spring.io/plugins-snapshot";
            content {
                includeGroupByRegex "io\\.spring\\.gradle\\.*"
            }
        }
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.2.0'
        classpath 'io.spring.gradle:propdeps-plugin:0.0.8-SNAPSHOT'
        classpath 'org.owasp:dependency-check-gradle:8.2.1'
    }
};

plugins {
    id("net.ltgt.errorprone") version "0.8.1" apply false
}

configure(allprojects) {
    apply(plugin: "propdeps");
    apply(plugin: "propdeps-maven");
    apply(plugin: "propdeps-idea");
    apply(plugin: "propdeps-eclipse");
}

apply(plugin: "java");
apply(plugin: "maven");
apply(plugin: "biz.aQute.bnd.builder");
apply(plugin: "idea");
apply(plugin: "eclipse");
apply(plugin: "net.ltgt.errorprone");
apply(plugin: 'org.owasp.dependencycheck');
apply(from: "project.gradle");

group = "com.github.java-json-tools";

/*
 * Repositories to use
 */
repositories {
    mavenCentral();
}

/*
 * Add errorprone checking.
 */
dependencies {
    errorprone('com.google.errorprone:error_prone_core:2.20.0')
    errorproneJavac("com.google.errorprone:javac:9+181-r4173-1")
}

/*
 * Necessary! Otherwise TestNG will not be used...
 *
 */
test {
    useTestNG() {
        useDefaultListeners = true;
    };
}

/*
 * Necessary to generate the source and javadoc jars
 */
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources";
    from sourceSets.main.allSource;
}

/*
 * Lint all the things!
 */
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all" << "-Xlint:-serial" << "-Werror"
        }
        tasks.withType(Javadoc) {
            options.addStringOption('Xwerror', '-quiet')
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc";
    from javadoc.destinationDir;
}

artifacts {
    archives jar;
    archives sourcesJar;
    archives javadocJar;
}


wrapper {
    gradleVersion = "5.6.3";
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip";
}


